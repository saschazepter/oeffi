plugins {
    id 'com.android.application'
    id 'androidsvgdrawable'
}

configurations {
    all*.exclude group: 'org.json', module: 'json'
    all*.exclude group: 'net.sf.kxml', module: 'kxml2'
    all*.exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk7'
    all*.exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
}

dependencies {
    implementation(project(':public-transport-enabler')) {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    implementation 'androidx.annotation:annotation:1.8.2'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'
    implementation 'androidx.drawerlayout:drawerlayout:1.2.0'
    implementation 'androidx.core:core:1.13.1'
    implementation 'androidx.activity:activity:1.9.3'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'
    implementation 'org.osmdroid:osmdroid-android:6.1.20'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    //noinspection GradleDependency
    implementation 'org.slf4j:slf4j-api:2.0.9'
    implementation 'com.github.tony19:logback-android:3.0.0'
    testImplementation 'junit:junit:4.13.2'
}

android {
    compileSdkVersion 'android-36'
    buildToolsVersion '36.1.0'

    buildTypes {
        all {
            minifyEnabled true
            shrinkResources true
            crunchPngs false
            proguardFile 'proguard.cfg'
        }
        debug {
            debuggable true
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
        test {
            java.srcDirs = ['test']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/*.version'
        exclude 'META-INF/proguard/**'
        exclude 'META-INF/services/**'
        exclude 'META-INF/*.kotlin_module'
        exclude 'META-INF/kotlin-project-structure-metadata.json'
        exclude 'META-INF/**/coroutines.pro'
        exclude 'DebugProbesKt.bin'
        exclude 'kotlin/**'
        exclude '**/*.kotlin_metadata'
        exclude 'okhttp3/internal/publicsuffix/NOTICE'
        exclude 'commonMain/default/**'
        exclude 'nonJvmMain/default/**'
    }
}

task svgToPng(type: fr.avianey.androidsvgdrawable.gradle.SvgDrawableTask) {
    from = files('graphics')
    to = file('res')
    targetedDensities = ['xxhdpi', 'xxxhdpi']
    outputFormat = 'PNG'
    outputType = 'mipmap'
}
